definition {

	macro executeSearchActions {
		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Search");

		var alertInfo = selenium.getText("//div[contains(@class,'alert alert-info')]");

		var vendor = StringUtil.extractFirst("${alertInfo}", ",");
		var version = StringUtil.extractLast("${alertInfo}", "Version:");

		var searchClientVersion = StringUtil.extractFirst("${version}", ",");
		var searchEngineVendor = StringUtil.extractLast("${vendor}", "Vendor:");

		if (contains("${searchEngineVendor}", "Elasticsearch")) {
			if (contains("${searchClientVersion}", "7.") && contains("${searchEngineVendor}", "Embedded")) {
				var curl = "http://localhost:9201/_tasks";
			}
			else {
				var curl = "http://localhost:9200/_tasks";
			}
		}

		Panel.expandPanel(panel = "Index Actions");

		AssertClick(
			key_actionsDescription = "${actionsDescription}",
			locator1 = "Button#EXECUTE_ACTION",
			value1 = "Execute");

		var reindexElapsedTime = "0";
		var reindexStartTime = "0";

		if (contains("${searchEngineVendor}", "Elasticsearch")) {
			echo("Current Elasticsearch Tasks:");

			var elasticsearchTasks = JSONCurlUtil.get("${curl}");

			while ((!(contains("${elasticsearchTasks}", "indices:data/write/bulk"))) && ("${reindexStartTime}" != "10")) {
				Pause.pauseNoSPARefresh(locator1 = "1000");

				var reindexStartTime = ${reindexStartTime} + 1;

				echo("Current Elasticsearch Tasks:");

				elasticsearchTasks = JSONCurlUtil.get("${curl}");
			}

			if ("${reindexStartTime}" == "10") {
				echo("Elasticsearch failed to receive reindex request after 10 seconds.");
			}
			else {
				echo("Started reindexing ${reindexStartTime} second(s) after clicking 'Execute'.");
			}

			while (contains("${elasticsearchTasks}", "indices:data/write/bulk") && ("${reindexElapsedTime}" != "120")) {
				Pause.pauseNoSPARefresh(locator1 = "5000");

				var reindexElapsedTime = ${reindexElapsedTime} + 5;

				echo("Current Elasticsearch Tasks:");

				elasticsearchTasks = JSONCurlUtil.get("${curl}");
			}
		}
		else if (contains("${searchEngineVendor}", "Solr")) {
			while (IsElementPresent(locator1 = "ControlMenu#RELOAD_TOOLTIP")&& ("${reindexElapsedTime}" != "120")) {
				Pause.pauseNoSPARefresh(locator1 = "5000");

				var reindexElapsedTime = ${reindexElapsedTime} + 5;
			}
		}

		if ("${reindexElapsedTime}" == "120") {
			fail("Reindex timed out after 120 seconds.");
		}
		else {
			echo("Finished reindexing after ${reindexElapsedTime} seconds.");
		}
	}

}